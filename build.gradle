plugins {
    id 'java'
    id 'idea'
    id 'eclipse'

    id 'org.openjfx.javafxplugin' version '0.0.7'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

version '1.0-SNAPSHOT'

sourceCompatibility = 11.0

test {
    useJUnitPlatform()
    reports {
        html.enabled = true
    }
}

repositories {
    mavenCentral()
}

// Yay! JavaFX!
javafx {
    // Java 11
    version = "11"

    modules = [
            // Include graphics so the main class does not need to extend javafx
            'javafx.graphics',

            // For WebView and swing integration.
            'javafx.web',
            'javafx.swing'
    ]
}

dependencies {
    testCompile("org.junit.platform:junit-platform-launcher:1.3.2")
    testCompile("org.junit.jupiter:junit-jupiter-engine:5.3.2")
    testCompile("org.junit.jupiter:junit-jupiter-params:5.3.2")
    testCompile("org.junit.vintage:junit-vintage-engine:5.3.2")

    // Core dependencies
    compile("com.google.guava:guava:27.1-jre")
    compile("org.apache.commons:commons-lang3:3.9")
    compile("org.apache.commons:commons-text:1.6")

    // Jetbrains IDE libraries.
    compile("com.github.adedayo.intellij.sdk:forms_rt:142.1")
    compile("org.jetbrains:annotations:17.0.0")

    // Graph-viz in java
    compile("ch.qos.logback:logback-classic:1.2.3")
    compile("guru.nidi:graphviz-java:0.8.3")
}

jar {
    manifest {
        attributes(
                "Class-Path": configurations.compile.collect { it.getName() }.join(' '),
                "Main-Class" : "uk.jixun.project.Entry"
        )
    }
}

task copyToLib(type: Copy) {
    into "$buildDir/libs"
    from configurations.runtime
}

jar.dependsOn(copyToLib)
